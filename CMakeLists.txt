cmake_minimum_required(VERSION 2.8)
project(ArxFatalis)

##############################################################################
# enable_unity_build
function(enable_unity_build UB_SUFFIX SOURCE_VARIABLE_NAME)  
	set(files ${${SOURCE_VARIABLE_NAME}})
	
	# Sort the file list to ensure we get the same files order on all machines
	list(SORT files)
	
	# Generate a unique filename for the unity build translation unit  
	set(unit_build_file ${CMAKE_CURRENT_BINARY_DIR}/ub_${UB_SUFFIX}.cpp)  
	
	# Exclude all translation units from compilation  
	set_source_files_properties(${files} PROPERTIES HEADER_FILE_ONLY true)  
	
	# Open the ub file  
	file(WRITE ${unit_build_file} "// Unity build generated by CMake\n\n")  
	
	# Add include statement for each translation unit
	list(LENGTH files numfiles)
	set(currentIdx 1)
	foreach(source_file ${files})  
		string(REGEX REPLACE ".*\\/" "" short_file "${source_file}")
		file(APPEND ${unit_build_file} "#pragma message (\"[${currentIdx}/${numfiles}] Compiling ${short_file}...\")\n")
		file(APPEND ${unit_build_file} "#include \"${source_file}\"\n\n")  
		math(EXPR currentIdx "${currentIdx} + 1")
	endforeach(source_file)  
	
	# Complement list of translation units with the name of ub  
	set(${SOURCE_VARIABLE_NAME} ${${SOURCE_VARIABLE_NAME}} ${unit_build_file} PARENT_SCOPE)  
endfunction(enable_unity_build) 

##############################################################################
# Check that the configured CMAKE_CXX_COMPILER works.
function(check_compiler)
	if(NOT CHECK_COMPILER)
		message(STATUS "Checking compiler: ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS}")
		
		# See if we can link a simple 32-bit program with the library.
		set(compile_test_file ${CMAKE_CURRENT_BINARY_DIR}/compile_test.cpp)
		file(WRITE ${compile_test_file} "int main(){}\n")
		try_compile(CHECK_COMPILER ${CMAKE_BINARY_DIR} ${compile_test_file} CMAKE_FLAGS "-DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER}" "-DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}" OUTPUT_VARIABLE ERRORLOG)
		
		if(NOT CHECK_COMPILER)
			message(FATAL_ERROR "\n${ERRORLOG}\n\n!! Compiler does not work, tried ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS}\n")
		endif()
	endif()
endfunction(check_compiler)

function(try_link_library LIBRARY_NAME LIBRARY_FILE ERROR_VAR)
	# See if we can link a simple program with the library using the configured c++ compiler.
	set(link_test_file ${CMAKE_CURRENT_BINARY_DIR}/link_test.cpp)
	file(WRITE ${link_test_file} "int main(){}\n")
	try_compile(CHECK_${LIBRARY_NAME}_LINK ${CMAKE_BINARY_DIR} ${link_test_file} CMAKE_FLAGS "-DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER}" "-DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}" "-DLINK_LIBRARIES=pthread ${LIBRARY_FILE}" OUTPUT_VARIABLE ERRORLOG)
	set(${ERROR_VAR} "${ERRORLOG}" PARENT_SCOPE)
endfunction(try_link_library)

##############################################################################
# Check that a a library actually works for the current configuration.
function(check_link_library LIBRARY_NAME LIBRARY_VARIABLE)
	
	set(lib_current "${${LIBRARY_VARIABLE}}")
	set(found_var "ARX_CLL_${LIBRARY_NAME}_FOUND")
	set(working_var "ARX_CLL_${LIBRARY_NAME}_WORKING")
	
	if(CHECK_${LIBRARY_NAME}_LINK)
		set(lib_found "${${found_var}}")
		set(lib_working "${${working_var}}")
		if((lib_current STREQUAL lib_found) OR (lib_current STREQUAL lib_working))
			set("${LIBRARY_VARIABLE}" "${lib_working}" PARENT_SCOPE)
			return()
		endif()
	endif()
	
	set("${found_var}" "${lib_current}" CACHE INTERNAL "...")
	
	message(STATUS "Checking ${LIBRARY_NAME}: ${lib_current}")
	
	# Check if we can link to the full path found by find_package.
	try_link_library(${LIBRARY_NAME} "${lib_current}" ERRORLOG1)
	
	if(CHECK_${LIBRARY_NAME}_LINK)
		set("${working_var}" "${lib_current}" CACHE INTERNAL "...")
		return()
	endif()
	
	# Check if the linker is smarter than cmake and try to link with only the library name.
	string(REGEX REPLACE "^.*/lib" "-l" LIBRARY_FILE ${lib_current})
	string(REGEX REPLACE ".so$" "" LIBRARY_FILE ${LIBRARY_FILE})
	try_link_library(${LIBRARY_NAME} ${LIBRARY_FILE} ERRORLOG2)
	
	if(CHECK_${LIBRARY_NAME}_LINK)
		message(STATUS " -> using ${LIBRARY_FILE} instead")
		set("${LIBRARY_VARIABLE}" "${LIBRARY_FILE}" PARENT_SCOPE)
		set("${working_var}" "${LIBRARY_FILE}" CACHE INTERNAL "...")
		return()
	endif()
	
	# Force cmake to search again, as the cached library doesn't work.
	unset(FIND_PACKAGE_MESSAGE_DETAILS_${ARGV2} CACHE)
	unset(FIND_PACKAGE_MESSAGE_DETAILS_${LIBRARY_NAME} CACHE)
	
	message(FATAL_ERROR "\n${ERRORLOG1}\n\n${ERRORLOG2}\n\n!! No suitable (32- vs. 64-bit) version of ${LIBRARY_NAME} found; tried ${lib_current} and ${LIBRARY_FILE}\nusing compiler ${CMAKE_CXX_COMPILER} ${CMAKE_CXX_FLAGS}\n")
	
endfunction(check_link_library)

function(force_recheck_library LIBRARY_NAME)
	unset(FIND_PACKAGE_MESSAGE_DETAILS_${ARGV1} CACHE)
	unset(FIND_PACKAGE_MESSAGE_DETAILS_${LIBRARY_NAME} CACHE)
	unset(CHECK_${LIBRARY_NAME}_LINK CACHE)
endfunction()

function(create_source_groups)
	# Source groups
	set(SOURCE_GROUPS ai animation audio core game graphics gui io physics platform scene scripting window)
	foreach(SOURCE_GROUP ${SOURCE_GROUPS})
		file(GLOB_RECURSE SOURCES ${SRC_DIR}/${SOURCE_GROUP}/*.*)
		source_group("${SOURCE_GROUP}" FILES ${SOURCES})
	endforeach(SOURCE_GROUP)
endfunction()

##############################################################################

option(ARX_BUILD_TESTS "Build tests" OFF)
option(ARX_BUILD_TOOLS "Build tools" ON)
option(ARX_MINGW "Build on MinGW" OFF)
option(ARX_USE_UNITYBUILD "Unity build" OFF)
option(ARX_DEBUG "Normal debug options" ON)
option(ARX_DEBUG_EXTRA "Expensive debug options" OFF)
option(ARX_FORCE_32BIT "Force a 32-bit build on 64-bit systems" ON)
option(ARX_USE_OPENAL "Build the OpenAL audio backend." ON)
option(ARX_USE_DSOUND "Build the DirectSound audio backend." ON)

if(NOT LAST_ARX_FORCE_32BIT EQUAL ARX_FORCE_32BIT)
	unset(CHECK_COMPILER CACHE)
	force_recheck_library(DevIL IL)
	force_recheck_library(ZLIB)
	force_recheck_library(Freetype)
	force_recheck_library(Threads)
	force_recheck_library(OpenAL)
	set(LAST_ARX_FORCE_32BIT ${ARX_FORCE_32BIT} CACHE INTERNAL "The last ARX_FORCE_32BIT value")
endif()

# For custom cmake modules.
set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/")

find_package(Freetype REQUIRED)
find_package(DevIL REQUIRED)
find_package(ZLIB REQUIRED)
find_package(Threads REQUIRED)
if(ARX_USE_OPENAL)
	find_package(OpenAL 1.1 REQUIRED)
	find_package(OpenALEFX)
endif()
find_package(Doxygen)
find_package(PythonInterp)

mark_as_advanced(IL_INCLUDE_DIR)
mark_as_advanced(ILUT_LIBRARIES)
mark_as_advanced(ILU_LIBRARIES)
mark_as_advanced(IL_LIBRARIES)

set(SRC_DIR src)
set(EXECUTABLE_OUTPUT_PATH ${CMAKE_SOURCE_DIR}/bin)
if(MSVC)
	# Set optimal options for msvc:

	# Remove targets we don't really use (RelWithDebInfo, MinSizeRel)
	set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "Using only Debug and Release" FORCE)
	
	# Disable deprecation warnings
	add_definitions(-D_CRT_SECURE_NO_WARNINGS)
	add_definitions(-D_CRT_NONSTDC_NO_DEPRECATE)
	add_definitions(-D_SCL_SECURE_NO_WARNINGS)
	add_definitions(/wd4995)
	
	# TEMP - disable warning caused by the F2L removal
	add_definitions(/wd4244)    # conversion from 'float' to 'long', possible loss of data

	# This is needed to use ZLIB as a DLL
	add_definitions(-DZLIB_WINAPI)
	
	# Add dx7 include & lib paths
	if("$ENV{DX7SDK_PATH}" STREQUAL "")
		message(WARNING "DX7SDK_PATH environment variable not found")
	else("$ENV{DX7SDK_PATH}" STREQUAL "")
		message(STATUS "Using DirectX 7 SDK found in " $ENV{DX7SDK_PATH})
		include_directories("$ENV{DX7SDK_PATH}/include")
		link_directories("$ENV{DX7SDK_PATH}/lib")
	endif("$ENV{DX7SDK_PATH}" STREQUAL "")
	
else(MSVC)
	# Set optimal options for gcc:
	
	find_package(Wine REQUIRED)
	
	# wine compiler
	set(CMAKE_CXX_COMPILER ${Wine_GXX})
	set(CMAKE_C_COMPILER ${Wine_GCC})
	
	if(ARX_FORCE_32BIT)
		set(CMAKE_C_FLAGS "-m32 ${CMAKE_C_FLAGS}")
		set(CMAKE_CXX_FLAGS "-m32 ${CMAKE_CXX_FLAGS}")
	endif()
	
	# since we change the compiler after it was checked by CMake, manually check if it works
	check_compiler()
	
	# Check the dependencies so there won't by cryptic link errors later on when found libraries have the wrong architecture (32- vs. 64-bit)
	check_link_library(Threads CMAKE_THREAD_LIBS_INIT)
	check_link_library(DevIL IL_LIBRARIES IL)
	check_link_library(ZLIB ZLIB_LIBRARIES)
	check_link_library(Freetype FREETYPE_LIBRARIES)
	if(ARX_USE_OPENAL)
		check_link_library(OpenAL OPENAL_LIBRARY)
	endif()
	
	# Warning level
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wformat=2 -Wundef -Wpointer-arith -Wlogical-op -Wcast-qual")
	
	# Consider also: -Wconversion -Wsign-conversion
	#                -Wmissing-declarations     to catch functions that should be marked as static
	#                -Wredundant-decls          to catch extern definitions in .cpp files (with UNITYBUILD)
	#                -Wsuggest-attribute=const  to add missing const qualifiers (only in gcc 4.6)
	
	if(ARX_DEBUG)
		#Debug
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -O0 -g3")
		add_definitions(-D_DEBUG)
	else()
		#Release
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -O2 -fno-strict-aliasing -g3") # TODO enable optimizations
		add_definitions(-D_NDEBUG)
	endif()
	
	if(ARX_DEBUG_EXTRA)
		add_definitions(-D_GLIBCXX_DEBUG) # Runtime checks for STL containers.
	endif()
	
	if(NOT ARX_DEBUG_EXTRA)
		# -Wuninitialized causes too many false positives
		# TODO Disable for now untill we find a way to suppress individual warnings.
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-uninitialized")
	endif()
	
	if(ARX_USE_UNITYBUILD)
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fwhole-program")
	endif()
	
endif(MSVC)

#use posix threads
set(ARX_THREAD_LIBS "${CMAKE_THREAD_LIBS_INIT}")
if(CMAKE_USE_PTHREADS_INIT)
	add_definitions(-DHAVE_PTHREADS)
	set(ARX_THREAD_LIBS "${ARX_THREAD_LIBS} -lrt")
elseif(NOT CMAKE_USE_WIN32_THREADS_INIT)
	message(SEND_ERROR "No supported thread libraries found.")
endif()

if(ARX_MINGW)
	set(CMAKE_CXX_COMPILER i486-mingw32-g++)
	set(CMAKE_C_COMPILER i486-mingw32-gcc)
endif(ARX_MINGW)

# Preprocessor definitions
add_definitions(-DNOMINMAX)

#glob sources
file(GLOB_RECURSE AI_SOURCES ${SRC_DIR}/ai/*.cpp)
file(GLOB_RECURSE ANIMATION_SOURCES ${SRC_DIR}/animation/*.cpp)
file(GLOB AUDIO_SOURCES ${SRC_DIR}/audio/*.cpp ${SRC_DIR}/audio/codec/*.cpp)
file(GLOB AUDIO_OPENAL_SOURCES ${SRC_DIR}/audio/openal/*.cpp)
file(GLOB AUDIO_DSOUND_SOURCES ${SRC_DIR}/audio/dsound/*.cpp)
file(GLOB_RECURSE CORE_SOURCES ${SRC_DIR}/core/*.cpp)
file(GLOB_RECURSE GAME_SOURCES ${SRC_DIR}/game/*.cpp)
file(GLOB_RECURSE GRAPHICS_SOURCES ${SRC_DIR}/graphics/*.cpp)
file(GLOB_RECURSE GUI_SOURCES ${SRC_DIR}/gui/*.cpp)
file(GLOB_RECURSE IO_SOURCES ${SRC_DIR}/io/*.cpp)
file(GLOB_RECURSE PHYSICS_SOURCES ${SRC_DIR}/physics/*.cpp)
file(GLOB_RECURSE PLATFORM_SOURCES ${SRC_DIR}/platform/*.cpp)
file(GLOB_RECURSE SCENE_SOURCES ${SRC_DIR}/scene/*.cpp)
file(GLOB_RECURSE SCRIPTING_SOURCES ${SRC_DIR}/scripting/*.cpp)
file(GLOB_RECURSE WINDOW_SOURCES ${SRC_DIR}/window/*.cpp)
file(GLOB_RECURSE ALL_INCLUDES ${SRC_DIR}/*.h)

set(ALL_SOURCES ${AI_SOURCES} ${ANIMATION_SOURCES} ${AUDIO_SOURCES} ${CORE_SOURCES} ${GAME_SOURCES} ${GRAPHICS_SOURCES} ${GUI_SOURCES} ${IO_SOURCES} ${PHYSICS_SOURCES} ${PLATFORM_SOURCES} ${SCENE_SOURCES} ${SCRIPTING_SOURCES} ${WINDOW_SOURCES})

include_directories(${SRC_DIR} ${ZLIB_INCLUDE_DIRS} ${IL_INCLUDE_DIR} ${FREETYPE_INCLUDE_DIRS})

if(ARX_USE_OPENAL)
	set(ALL_SOURCES ${ALL_SOURCES} ${AUDIO_OPENAL_SOURCES})
	include_directories(${OPENAL_INCLUDE_DIR})
	add_definitions(-DHAVE_OPENAL)
	if(OPENALEFX_FOUND)
		include_directories(${OPENAL_EFX_INCLUDE_DIR})
		add_definitions(-DHAVE_OPENAL_EFX)
	endif()
endif()
if(ARX_USE_DSOUND)
	set(ALL_SOURCES ${ALL_SOURCES} ${AUDIO_DSOUND_SOURCES})
	add_definitions(-DHAVE_DSOUND)
endif()

#enable unity build if requested
if(ARX_USE_UNITYBUILD)
	enable_unity_build(arx ALL_SOURCES)
endif()

#create source groups
create_source_groups()

#build
add_executable(arx ${ALL_SOURCES} ${ALL_INCLUDES})

#link
set(ARX_WINDOWS_LIBS gdi32 shell32 comdlg32 ole32 comctl32)
set(ARX_GRAPHICS_LIBS dxguid dinput ddraw)
target_link_libraries(arx ${ARX_GRAPHICS_LIBS} ${ARX_WINDOWS_LIBS} ${FREETYPE_LIBRARIES} ${ZLIB_LIBRARIES} ${IL_LIBRARIES} ${ARX_THREAD_LIBS})
if(ARX_USE_OPENAL)
	target_link_libraries(arx ${OPENAL_LIBRARY})
endif()

add_custom_target(remake
	#clean and compile with 1 thread per core
	COMMAND make clean && rm CMakeCache.txt && cmake ${CMAKE_SOURCE_DIR} -G\"Unix Makefiles\" && make -j`getconf _NPROCESSORS_ONLN`
)

if(DOXYGEN_EXECUTABLE)
	add_custom_target(doc
		#build the documentation
		COMMAND cd ${CMAKE_SOURCE_DIR} && ${DOXYGEN_EXECUTABLE} doc/Doxyfile
	)
endif()

#find python2
if(PYTHONINTERP_FOUND)
	set(STYLE_FILTER -readability/casting,-build/header_guard,-build/include,-readability/streams,-whitespace,-runtime/int)
	add_custom_target(style
		COMMAND ${PYTHON_EXECUTABLE} ${CMAKE_SOURCE_DIR}/scripts/cpplint.py --filter=${STYLE_FILTER} ${ALL_SOURCES} ${ALL_INCLUDES}
	)
endif()

if(ARX_BUILD_TESTS)
	add_subdirectory(tests ${CMAKE_SOURCE_DIR}/bin/tests)
endif()

if(ARX_BUILD_TOOLS)
	add_subdirectory(tools ${CMAKE_SOURCE_DIR}/bin/tools)
endif()
